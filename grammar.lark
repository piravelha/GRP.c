
program: declaration* expression?

?declaration: variable

expression: atom+

?atom: if
     | array
     | symbol
     | symbol_call
     | quote
     | splice
     | NAME
     | OPERATOR
     | INT
     | STRING
     | CHAR
     | FLOAT
     | paren

paren: "(" expression ")"

if: "{" if_case* "else" expression "}"

if_case: "if" expression "then" expression

symbol: ":" SYMBOL

symbol_call: SYMBOL

quote: "{" expression "}"

splice: "${" expression "}"

variable: NAME ("(" OPERATOR ")")? "::" expression "::"

array: "[" atom* "]"

NAME: /[A-Z][a-zA-Z0-9]*/
SYMBOL: /(?!if|then|else)[a-z][a-zA-Z0-9]*/
OPERATOR: /(?!::)[+\-*\/^!@#$%&~,.:|?<>=_](?![{a-zA-Z0-9])[+\-*\/^!@#$%&~,.:|?<>=_]*/
INT: /-?\d+/
FLOAT: /-?\d+\.\d+/
STRING: /"[^"]*"/
CHAR: /'[^']'/

%ignore /\s+/
%ignore /;.*/